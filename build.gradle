buildscript {
    ext {
        artifactoryPluginVersion = '4.4.0'
        dependencyCheckVersion = '1.4.2'
        gradleLombokVersion = "1.11"
        sonarQubePluginVersion = '2.6.2'
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            uri 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "io.franzbecker:gradle-lombok:${gradleLombokVersion}"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:${artifactoryPluginVersion}"
        classpath "org.owasp:dependency-check-gradle:${dependencyCheckVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarQubePluginVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
    }
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'eclipse'
apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.sonarqube'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'
    compile group: 'com.googlecode.json-simple:json-simple', version: '1.1.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3'
    compile group: 'de.otto.edison', name: 'edison-jobs', version: '0.62.0'
    compile group: 'de.otto.edison', name: 'edison-service', version: '0.62.0'
    compile group: 'de.otto.edison', name: 'edison-togglz', version: '0.62.0'
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator-docs'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    compile group: 'org.zaproxy', name: 'zap', version: '2.7.0'
    compile group: 'org.zaproxy', name: 'zap-clientapi', version: '1.6.0'
}

compileJava.dependsOn(processResources)

jar {
    baseName = artifactName
    manifest.attributes provider: 'gradle'
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

artifactory {
    contextUrl = 'https://artifactory.iteratec.de/artifactory'
    publish {
        repository {
            repoKey = 'securebox-local'
            username = artifactory_user
            password = artifactory_password
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId artifactName
        }
    }
}

dependencyCheck {
    if (System.getenv('bamboo_cvss_limit') != null) {
        failBuildOnCVSS = (System.getenv('bamboo_cvss_limit')).toInteger()
    }
}

// Passing the active profile information in the case of a simple "gradle bootRun".
// Thanks to: https://github.com/spring-projects/spring-boot/pull/592https://github.com/spring-projects/spring-boot/pull/592
bootRun {
    systemProperties['spring.profiles.active'] = project.gradle.startParameter.systemPropertiesArgs['spring.profiles.active']
    systemProperties['spring.application.json'] = project.gradle.startParameter.systemPropertiesArgs['spring.application.json']
}

// File filtering and token replacement. Variables are defined in "gradle.properties" file and can be overridden e.g. via "-Pvcs_commit=${bamboo.repository.revision.number}".
// Thanks to: https://docs.gradle.org/current/userguide/build_environment.html
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            "application.version"        : project.property("version"),
            "application.vcs.commit"     : vcs_commit,
            "application.vcs.version"    : project.property("version"),
            "application.vcs.url"        : vcs_url,
            "application.build.url"      : build_url,
            "application.build.timestamp": build_timestamp
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
