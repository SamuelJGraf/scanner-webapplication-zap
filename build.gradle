buildscript {
    ext {
        gradleLombokVersion = "1.11"  // Problem in IntelliJ with 1.12
        sonarQubePluginVersion = '2.6.2'
        springBootVersion = '1.5.11.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            setUrl 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "io.franzbecker:gradle-lombok:${gradleLombokVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarQubePluginVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "de.sstoehr:har-reader:2.1.2"
    }
}

apply plugin: 'eclipse'
apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'de.otto.edison', name: 'edison-jobs', version: '1.2.30'
    compile group: 'de.otto.edison', name: 'edison-togglz', version: '1.2.30'
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator-docs'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    compile group: 'org.zaproxy', name: 'zap', version: '2.7.0'
    compile group: 'org.zaproxy', name: 'zap-clientapi', version: '1.6.0'
    compile group: 'de.sstoehr', name: 'har-reader', version: '2.1.2'

    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

compileJava.dependsOn(processResources)

jar {
    baseName = project.name
    manifest.attributes provider: 'gradle'
}

// Passing the active profile information in the case of a simple "gradle bootRun".
// Thanks to: https://github.com/spring-projects/spring-boot/pull/592
bootRun.systemProperties = System.properties

// File filtering and token replacement. Variables are defined in "gradle.properties" file and can be overridden e.g. via "-Pvcs_commit=${repository.revision.number}".
// Thanks to: https://docs.gradle.org/current/userguide/build_environment.html
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            "application.version"        : project.property("version"),
            "application.vcs.commit"     : vcs_commit,
            "application.vcs.version"    : project.property("version"),
            "application.vcs.url"        : vcs_url,
            "application.build.url"      : build_url,
            "application.build.timestamp": build_timestamp
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
